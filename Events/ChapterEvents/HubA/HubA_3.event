#define A3LogFlag 7
#define A3Spawn1AreaFlag 8
#define A3Spawn2AreaFlag 9
#define A3Spawn1TurnFlag 10
#define A3Spawn2TurnFlag 11
#define A3Spawn3TurnFlag 12
#define A3Spawn3AreaFlag 13
// 14, 15, 16, 17 are battle quotes

EventPointerTable(HubA_3EventsID, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units2
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, A3Explanation, 1)
TurnEventPlayer(A3Spawn1TurnFlag, A3Reinforce1, 1, 255) //bottom left fort spawn
TurnEventPlayer(A3Spawn2TurnFlag, A3Reinforce2, 1, 255)
TurnEventPlayer(A3Spawn3TurnFlag, A3Reinforce3, 1, 255)
TurnEventPlayer(0, SpiritWarning, 7)
Survive(GameOverYeaaaaah,13)
END_MAIN

SpiritWarning:
CAMERA_CENTERED [0,0]
STAL 10
CURSOR [4,0]
SOUN 0x02F7 //druid crit charge, but supposed to be sound of phantoms 
STAL 40
REMOVE_CURSORS
STAL 30
CameraCursor(Lua)

CHECK_ALIVE Biko
BEQ 775 s0 sC
	CHECK_ALIVE Ondeh
	BEQ 775 s0 sC
		Text(A3_MidBattle_PhantomWarn_AllUnits) //If both Biko and Ondeh are alive, then play cutscene with all of them.
		GOTO 776
LABEL 775
Text(A3_MidBattle_PhantomWarn_LuaSolo) // else do cutscene with only Lua
LABEL 776
NoFade
ENDA

A3Explanation:
CAMERA_CENTERED [18,14]
STAL 30
CenterTextbox(ChA3ObjectiveExplanation)
PanToTileThenFlashCursorForXSeconds(18, 0, 30)
PanToTileThenFlashCursorForXSeconds(10, 9, 30)
PanToTileThenFlashCursorForXSeconds(0, 14, 30)
CenterTextbox(ChA3SideObjectiveExplanation)
NoFade
ENDA

GameOverYeaaaaah:
CameraCursor(Lua)
Text(A3_SanTorino_Appears)
CALL GameOverStart
NoFade
ENDA

A3Reinforce1:
SpawnReinforcements(Reinforce1)
NoFade
ENDA

Reinforce1:
UNIT RiojaReaper Myrmidon 0 Level(3, Enemy, True) [16,7] 0 0 1 REDA16R7 [Onigiri] PursueWithoutHeedAI
UNIT

A3Reinforce2:
SpawnReinforcements(Reinforce2)
NoFade
ENDA

Reinforce2:
UNIT RiojaReaper Deserter 0 Level(3, Enemy, True) [7,14] 0 0 1 REDA7R14 [SteelLance] PursueWithoutHeedAI
UNIT

A3Reinforce3:
SpawnReinforcements(Reinforce3)
NoFade
ENDA

Reinforce3:
UNIT RiojaReaper Fighter 0 Level(3, Enemy, True) [15,14] 0 0 1 REDA14R14 [SteelAxe] PursueWithoutHeedAI
UNIT RiojaReaper Mercenary 0 Level(3, Enemy, True) [15,14] 0 0 1 REDA15R13 [SteelSword] PursueWithoutHeedAI
UNIT

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Escape(18,13)
Escape(18,14)
Escape(18,15)
END_MAIN

MiscBasedEvents:
AREA A3Spawn1AreaFlag CheckIfTimeForSpawn1 [12, 2] [18,9]
AREA A3Spawn2AreaFlag CheckIfTimeForSpawn2 [4, 12] [9,16]
AREA A3Spawn3AreaFlag CheckIfTimeForSpawn3 [11, 11] [18,16]
AFEV 0 CheckIfLogDroppedOnCorrectTile 0
AFEV A3LogFlag CheckIfLogsInPlace 0
CauseGameOverIfLordDies
AFEV 0x0 CheckNoUnits 0x0
END_MAIN

CheckIfTimeForSpawn1:
CheckIfTimeForTurnSpawnEnabledByAreaEventPlayer(1, 2, A3Spawn1AreaFlag, A3Spawn1TurnFlag)
NoFade
ENDA

CheckIfTimeForSpawn2:
CheckIfTimeForTurnSpawnEnabledByAreaEventPlayer(1, 2, A3Spawn2AreaFlag, A3Spawn2TurnFlag)
NoFade
ENDA

CheckIfTimeForSpawn3:
CheckIfTimeForTurnSpawnEnabledByAreaEventPlayer(1, 2, A3Spawn3AreaFlag, A3Spawn3TurnFlag)
NoFade
ENDA

CheckIfLogsInPlace:
BranchIfNotUnitAtCoords(0, 14, A3Log, 1)
BranchIfNotUnitAtCoords(10, 9, A3Log, 1)
BranchIfNotUnitAtCoords(18, 0, A3Log, 1)
CALL ForestFriendEvent //if all 3 logs at the specified tiles, call forest friend event
GOTO 2
LABEL 1
ENUF A3LogFlag
LABEL 2
NoFade
ENDA

ForestFriendEvent:
RemoveAllThreeLogs
Text(A3_ForestFriendText)
GiveItemWithDurability(SkillScroll,ForestFriendID,0xFFFF)
NoFade
ENDA

CheckIfLogDroppedOnCorrectTile:
A3ReturnLogDroppedStatus
BEQ 1 s0 sC
    SVAL s1 1
    BNE 2 s1 sC
        Text(A3FirstLogDropped)
        GOTO 1
    LABEL 2
    SVAL s1 2
    BNE 3 s1 sC
        Text(A3SecondLogDropped)
        GOTO 1
    LABEL 3
    Text(A3ThirdLogDropped)
LABEL 1
NoFade
ENDA

CheckNoUnits:
GotoEndingSceneIfNoPlayers
NoFade
ENDA

CutsceneUnits:
UNIT Lua 0 0 Level(10,Ally,False) [2,15]  0 0 1 REDA2R6 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Ondeh 0 0 Level(4,NPC,False) [2,16]  0x0 0 1 REDA3R7 [ShiningBow,IronBow,Vulnerary] NoAI
UNIT Biko 0 0 Level(1,NPC,False) [3,16]   0x0 0 1 REDA1R7 [IronLance,Vulnerary] NoAI
UNIT

Units1:
UNIT Lua 0 0 Level(10,Ally,False) [2,6] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Ondeh 0 0 Level(4,Ally,False) [1,5] 0x0 0x0 0 0 [ShiningBow,IronBow,Vulnerary] NoAI
UNIT Biko 0 0 Level(1,Ally,False) [3,5] 0x0 0x0 0 0 [IronLance,Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [2,5] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [1,4] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [2,4] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [3,4] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [1,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [2,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [3,3] 0 0 0 0 [Vulnerary] NoAI
UNIT

Units2:
UNIT Lua 0 0 Level(10,Ally,False) [2,6] 0 0 0 0 [Cutlass, IronSword, IronMusket, Vulnerary] NoAI
UNIT Ondeh 0 0 Level(4,Ally,False) [1,5] 0x0 0x0 0 0 [ShiningBow,IronBow,Vulnerary] NoAI
UNIT Biko 0 0 Level(1,Ally,False) [3,5] 0x0 0x0 0 0 [IronLance,Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [2,5] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [1,4] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [2,4] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [3,4] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [1,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [2,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [3,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [0,3] 0 0 0 0 [Vulnerary] NoAI
UNIT Pilar 0 0 Level(8,Ally,False) [4,3] 0 0 0 0 [Vulnerary] NoAI
UNIT

NewPlayerUnits:
UNIT Ondeh 0 0 Level(4,Ally,True) [1,5] 0x0 0x0 0 0 [ShiningBow,IronBow,Vulnerary] NoAI
UNIT Biko 0 0 Level(1,Ally,True) [3,5] 0x0 0x0 0 0 [IronLance,Vulnerary] NoAI
UNIT


Traps1:
BlueSigil(18,13)
BlueSigil(18,14)
BlueSigil(18,15)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BossGroup:
UNIT Sargargala 0 0 Level(7,Enemy,True) [18,14]  0 0 1 REDA17R14 [KillerLance, LightBrand] BossAI
UNIT RiojaReaper Vanguard 0 Level(3, Enemy, True) [18,13] 0 0 1 REDA17R13 [HandAxe] BossAI
UNIT RiojaReaper Vanguard 0 Level(3, Enemy, True) [18,15] 0 0 1 REDA17R15 [HandAxe] BossAI
UNIT

EnemyGroup:

UNIT RiojaReaper Deserter 0 Level(3, Enemy, True) [3,13] 0 0 1 REDA4R8 [SteelLance] PursueWithoutHeedAI

UNIT RiojaReaper Archer 0 Level(3, Enemy, True) [10,11] 0 0 1 REDA10R9 [Longbow, TealGem] AttackWhenInRangeAI

UNIT RiojaReaper Brigand 0 Level(3, Enemy, True) [10,14] 0 0 0 0 [HandAxe] BossAI
UNIT RiojaReaper Shaman 0 Level(3, Enemy, True) [10,15] 0 0 0 0 [Elfire] BossAI

UNIT RiojaReaper Shaman 0 Level(3, Enemy, True) [16,10] 0 0 0 0 [Elfire] BossAI
UNIT RiojaReaper Deserter 0 Level(3, Enemy, True) [17,10] 0 0 0 0 [Javelin] BossAI

UNIT RiojaReaper Fighter 0 Level(3, Enemy, True) [2,11] 0 0 0 0 [SteelAxe] GroupOneAI
UNIT RiojaReaper Archer 0 Level(4, Enemy, True) [0,13] 0 0 0 0 [Tomereaver, Longbow] GroupOneAI
UNIT RiojaReaper Myrmidon 0 Level(3, Enemy, True) [5,15] 0 0 0 0 [Onigiri] GroupOneAI
UNIT RiojaReaper Thief 0 Level(3, Enemy, True) [7,12] 0 0 0 0 [SteelKnife] AttackWhenInRangeAI

UNIT RiojaReaper Mage 0 Level(3, Enemy, True) [14,1] 0 0 1 REDA6R0 [Fire] GroupTwoAI
UNIT RiojaReaper Soldier 0 Level(4, Enemy, True) [14,4] 0 0 1 REDA10R4 [Pike, Pitchfork] GroupTwoAI
UNIT RiojaReaper Mercenary 0 Level(3, Enemy, True) [4,10] 0 0 1 REDA6R16 [SteelSword] GroupTwoAI
UNIT RiojaReaper Brigand 0 Level(3, Enemy, True) [15,0] 0 0 0 0 [IronAxe] GroupTwoAI

UNIT RiojaReaper Deserter 0 Level(3, Enemy, True) [15,5] 0 0 0 0 [SteelLance] GroupThreeAI
UNIT RiojaReaper Archer 0 Level(4, Enemy, True) [12,9] 0 0 0 0 [MiniBow, SteelBow] GroupThreeAI

UNIT RiojaReaper PegasusRider 0 Level(3, Enemy, True) [18,12] 0 0 0 0 [SteelSword] GroupFourAI
UNIT RiojaReaper GriffinRider 0 Level(3, Enemy, True) [18,16] 0 0 0 0 [SteelLance] GroupFiveAI
UNIT RiojaReaper GriffinRider 0 Level(3, Enemy, True) [12,12] 0 0 0 0 [IronLance] GroupFourAI
UNIT RiojaReaper PegasusRider 0 Level(3, Enemy, True) [12,16] 0 0 0 0 [IronSword] GroupFiveAI
UNIT RiojaReaper DragonRider 0 Level(4, Enemy, True) [15,14] 0 0 0 0 [SteelAxe, Hammer] AttackWhenInRangeAI
UNIT

LogGroup:
UNIT A3Log 0 0 Level(1,NPC, False) [1,8] 0 0 0 0 [0x0] BossAI
UNIT A3Log 0 0 Level(1,NPC, False) [3,10] 0 0 0 0 [0x0] BossAI
UNIT A3Log 0 0 Level(1,NPC, False) [6,9] 0 0 0 0 [0x0] BossAI
UNIT

BeginningScene:
MUSC 0x4B
SetBackground(Forest)
Text(A3_Scene1_HeadToTemple)
EraseBackground

LoadCutsceneUnits(CutsceneUnits)
CAMERA_CENTERED [3,4]
STAL 30 
CameraCursor(Lua)
SetBackground(Temple_Gate)
Text(A3_Scene2_TempleEntrance)
EVBIT_F 2 //Skipping stops here
MUSC 0x7FFF
EraseBackground

MOVE 6 Lua [2,2]
MOVE 6 Biko [1,2]
MOVE 6 Ondeh [3,2]
STAL 90
SetBackground(Temple_Ruins_Inside)
Text(A3_Scene3_InTheTomb)
EraseBackground

Music(SwabbieTalkID)
CAMERA_CENTERED [15,14]
LOAD1 1 BossGroup
ENUN
STAL 10
CameraCursor(Sargargala)
CAMERA_CENTERED [15,14]
SetBackground(Forest_2)
Text(A3_Scene4_Sagargala)
EraseBackground

CAMERA_CENTERED [3,4]
STAL 30
LOAD1 1 EnemyGroup
ENUN
STAL 10
CameraCursor(Lua)
MOVE 15 Lua [2,3]
ENUN
Text(A3_Scene5_ReadyForBattle)

FADI 15
CLEN 0 //erase the NPC biko and ondeh (sorry guys)
LOAD1 1 NewPlayerUnits
LOAD1 1 LogGroup
ENUN
ENUT A3Spawn1TurnFlag
ENUT A3Spawn2TurnFlag
ENUT A3Spawn3TurnFlag
ENUT LordMustEscapeLastFlag
GotoPrepScreen
NoFade
ENDA

EndingScene:
FadeoutMusic
SetBackground(Forest_2)
CHECK_ALIVE Biko
BEQ 775 s0 sC
	CHECK_ALIVE Ondeh
	BEQ 775 s0 sC
		Text(A3_Ending_AllAlive) //If both Biko and Ondeh are alive, then play cutscene with all of them.
		GOTO 776
LABEL 775
Text(A3_Ending_AllAlive) //if not, play the same cutscene because variations aren't written yet. 
LABEL 776
PlayEventIfEventIDSet(A3LogFlag, 1, AshArrowItemEvent)
ENUF LordMustEscapeLastFlag
GoToHubChapter(HubA)
NoFade
ENDA

AshArrowItemEvent:
Text(A3_AshQuiverEventText)
GiveItem(AshQuiver, Lua)
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN