EventPointerTable(HubA_1EventsID, PointerList)

// temporary flags
/*
#define CarmenIndioTalkSeen 0x7
#define PilarRazvanTalkSeen 0x8
#define LeandraTalkSeen 0x9
#define LeandraRecruited 0xA
#define RelicRecovered 0xB
#define ObjectiveSwitched 0xC
#define EscapeDisallowed 0xD

#define GeraldoLuaBattleQuoteSeen 0xE
#define GeraldoPilarBattleQuoteSeen 0xF
#define GeraldoDesiderioBattleQuoteSeen 0x10
#define GeraldoRazvanBattleQuoteSeen 0x11
#define GeraldoCarmenBattleQuoteSeen 0x12
#define GeraldoXimoBattleQuoteSeen 0x13
#define GeraldoEstelleBattleQuoteSeen 0x14
#define GeraldoHermeneBattleQuoteSeen 0x15

#define GeraldoGenericBattleQuoteSeen 0x28
*/
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units2
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, InformObjective, 1)
TurnEventEnemy(0, GeraldoLeaves, 1)
TurnEventPlayer(0, LuaNoticesChests, 2)
TurnEventEnemy(0, SuperbossCameo, 3)
TurnEventPlayer(0, FirstTimerWarning, 5)
TurnEventPlayer(0, SecondTimerWarning, 7)
TurnEventPlayer(0, AlarmRaised, 9)
TurnEventEnemy(0, SuperbossesAppear, 11)
TurnEventPlayer(0, YurenaDropping, 13)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(A1_CarmenIndioTalkSeen, CarmenIndioTalk, Carmen, Indio)
CharacterEventBothWays(A1_PilarRazvanTalkSeen, PilarRazvanTalk, Pilar, Razvan)
CharacterEventBothWays(A1_LeandraRecruited, LuaRecruitLeandra, Lua, Leandra)
CharacterEventBothWays(A1_LeandraRecruited, DesiderioRecruitLeandra, Desiderio, Leandra)
END_MAIN

LocationBasedEvents:
Escape(A1_EscapeDisallowed, EscapeEventCheck, 5, 2)
Escape(A1_EscapeDisallowed, EscapeEventCheck, 6, 2)
Escape(A1_EscapeDisallowed, EscapeEventCheck, 26, 8)
Escape(A1_EscapeDisallowed, EscapeEventCheck, 27, 8)
DurabilityChest(SkillScroll, CanterID, 17, 4)
ChestMoney(1500, 6, 15)
Chest(Bloodletter, 22, 15)
CHES A1_RelicRecovered Statuette [18, 7] 0x14 // Macro can't set a flag so this is the objective chest 
END_MAIN

MiscBasedEvents:
AFEV A1_ObjectiveSwitched SwitchObjective A1_RelicRecovered
AFEV 0x0 CheckNoUnits 0x0
CauseGameOverIfLordDies
END_MAIN

Units1:
UNIT Lua Captain 0 Level(1, Ally, False) [7,5] 0 0 0 0 [0, 0, 0, 0] NoAI 
UNIT Indio Shaman 0 Level(1, Ally, False) [25,8] 0 0 0 0 [Surge, Vulnerary] NoAI // Indio
UNIT Indio Shaman 0 Level(1, Ally, False) [26,9] 0 0 0 0 [Surge, Vulnerary] NoAI // Desiderio
UNIT Indio Shaman 0 Level(1, Ally, False) [6,4] 0 0 0 0 [Surge, Vulnerary] NoAI // Pilar
UNIT Indio Shaman 0 Level(1, Ally, False) [5,3] 0 0 0 0 [Surge, Vulnerary] NoAI // Razvan
UNIT Indio Shaman 0 Level(1, Ally, False) [27,8] 0 0 0 0 [Surge, Vulnerary] NoAI // Carmen
UNIT Indio Shaman 0 Level(1, Ally, False) [4,4] 0 0 0 0 [Surge, Vulnerary] NoAI // Ximo
UNIT Indio Shaman 0 Level(1, Ally, False) [26,7] 0 0 0 0 [Surge, Vulnerary] NoAI // Estelle
UNIT Indio Shaman 0 Level(1, Ally, False) [5,5] 0 0 0 0 [Surge, Vulnerary] NoAI // Hermene
UNIT

Units2:
UNIT Lua Captain 0 Level(1, Ally, False) [7,5] 0 0 0 0 [0, 0, 0, 0] NoAI 
UNIT Indio Shaman 0 Level(1, Ally, False) [25,8] 0 0 0 0 [Surge, Vulnerary] NoAI // Indio
UNIT Indio Shaman 0 Level(1, Ally, False) [26,9] 0 0 0 0 [Surge, Vulnerary] NoAI // Desiderio
UNIT Indio Shaman 0 Level(1, Ally, False) [6,4] 0 0 0 0 [Surge, Vulnerary] NoAI // Pilar
UNIT Indio Shaman 0 Level(1, Ally, False) [5,3] 0 0 0 0 [Surge, Vulnerary] NoAI // Razvan
UNIT Indio Shaman 0 Level(1, Ally, False) [27,8] 0 0 0 0 [Surge, Vulnerary] NoAI // Carmen
UNIT Indio Shaman 0 Level(1, Ally, False) [4,4] 0 0 0 0 [Surge, Vulnerary] NoAI // Ximo
UNIT Indio Shaman 0 Level(1, Ally, False) [26,7] 0 0 0 0 [Surge, Vulnerary] NoAI // Estelle
UNIT Indio Shaman 0 Level(1, Ally, False) [5,5] 0 0 0 0 [Surge, Vulnerary] NoAI // Hermene
UNIT Indio Shaman 0 Level(1, Ally, False) [28,7] 0 0 0 0 [Surge, Vulnerary] NoAI // Estelle
UNIT Indio Shaman 0 Level(1, Ally, False) [6,2] 0 0 0 0 [Surge, Vulnerary] NoAI // Hermene
UNIT

LuaDesiderioCutsceneUnits:
UNIT Lua Captain 0 Level(1, Ally, False) [0,3] 0 0 0 0 [0, 0, 0, 0] NoAI
UNIT Desiderio Vanguard 0 Level(1, Ally, False) [0,4] 0 0 0 0 [0, 0, 0, 0] NoAI
UNIT

IndioCutsceneUnit:
UNIT Indio Shaman 0 Level(1, Ally, True) [0,4] 0 0 1 REDA4R2 [Surge, Vulnerary] NoAI
UNIT

ContractorInitialEnemies:
UNIT Leandra Fighter_F Geraldo Level(5, Enemy, True) [11, 5] 0 0 0 0 [IronAxe, Swordreaver, Vulnerary] AttackInTwoAI
UNIT Geraldo Mercenary Geraldo Level(5, Enemy, False) [12, 5] 0 0 0 0 [SteelSword] AttackWhenInRangeAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, True) [10, 4] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, True) [12, 4] 0 0 0 0 [IronSword] AttackInTwoAI

//UNIT Contractor Myrmidon Geraldo Level(3, Enemy, True) [24, 10] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, True) [24, 12] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Hunter Geraldo Level(3, Enemy, True) [23, 11] 0 0 0 0 [IronBow] AttackInTwoAI

UNIT Contractor Myrmidon Geraldo Level(3, Enemy, True) [5, 8] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Myrmidon Geraldo Level(3, Enemy, True) [7, 8] 0 0 0 0 [IronSword] AttackInTwoAI
UNIT Contractor Hunter Geraldo Level(3, Enemy, True) [6, 9] 0 0 0 0 [IronBow] AttackInTwoAI

UNIT Contractor Shaman Geraldo Level(3, Enemy, True) [14, 16] 0 0 0 0 [Fire] GroupOneAI
//UNIT Contractor Shaman Geraldo Level(3, Enemy, True) [14, 16] 0 0 0 0 [Fire] GroupOneAI
UNIT Contractor AxeCav Geraldo Level(3, Enemy, True) [13, 17] 0 0 0 0 [IronAxe] GroupOneAI
UNIT Contractor LanceCav Geraldo Level(3, Enemy, True) [15, 17] 0 0 0 0 [IronLance] GroupOneAI
UNIT

SentinelInitialEnemies:
UNIT Buzu Vanguard Sanz Level(5, Enemy, True) [17, 7] 0 0 0 0 [HandAxe, Pike] BossAI
UNIT Bozo Warden Sanz Level(5, Enemy, True) [19, 7] 0 0 0 0 [FlameSword, Onigiri] BossAI

UNIT Sentinel Warden Sanz Level(4, Enemy, True) [6, 15] 0 0 0 0 [Javelin] BossAI
UNIT Sentinel Warden Sanz Level(4, Enemy, True) [22, 15] 0 0 0 0 [Javelin] BossAI
UNIT Sentinel Warden Sanz Level(4, Enemy, True) [17, 4] 0 0 0 0 [Javelin] BossAI

UNIT Sentinel Mercenary Sanz Level(4, Enemy, True) [14, 7] 0 0 0 0 [IronSword] AttackWhenInRangeAI
UNIT Sentinel Mercenary Sanz Level(4, Enemy, True) [14, 13] 0 0 0 0 [IronSword] AttackWhenInRangeAI
UNIT Sentinel Mercenary Sanz Level(4, Enemy, True) [16, 10] 0 0 0 0 [IronSword] AttackWhenInRangeAI
UNIT Sentinel Mage Sanz Level(4, Enemy, True) [17, 9] 0 0 0 0 [Elfire] AttackInTwoAI
UNIT Sentinel Mage Sanz Level(4, Enemy, True) [19, 9] 0 0 0 0 [Elfire] AttackInTwoAI

// these aren't sentinels but i ran out of space in the first unit group
UNIT Contractor Brigand Geraldo Level(3, Enemy, True) [7, 16] 0 0 0 0 [IronAxe] AttackInTwoAI
UNIT Contractor Brigand Geraldo Level(3, Enemy, True) [8, 15] 0 0 0 0 [IronAxe] AttackInTwoAI
UNIT Contractor Mage Geraldo Level(3, Enemy, True) [3, 14] 0 0 0 0 [Fire] AttackInTwoAI

UNIT Contractor Deserter Geraldo Level(3, Enemy, True) [21, 5] 0 0 0 0 [IronLance] AttackInTwoAI
//UNIT Contractor Deserter Geraldo Level(3, Enemy, True) [22, 4] 0 0 0 0 [IronLance] AttackInTwoAI
UNIT Contractor Mage Geraldo Level(3, Enemy, True) [20, 4] 0 0 0 0 [Fire] AttackInTwoAI
UNIT

UndergroundReinforcements:
UNIT Geraldo Mercenary Geraldo Level(5, Enemy, True) [4, 4] 0 0 0 0 [SteelSword] ChargeAI
UNIT Sentinel Archer Sanz Level(4, Enemy, True) [3, 10] 0 0 0 0 [IronBow] ChargeAI
UNIT Sentinel Archer Sanz Level(4, Enemy, True) [21, 12] 0 0 0 0 [IronBow] ChargeAI
UNIT Contractor Brigand Sanz Level(3, Enemy, True) [5, 16] 0 0 0 0 [IronAxe] ChargeAI
UNIT Contractor Myrmidon Sanz Level(3, Enemy, True) [7, 17] 0 0 0 0 [IronSword] ChargeAI
UNIT Contractor Brigand Sanz Level(3, Enemy, True) [22, 4] 0 0 0 0 [IronAxe] ChargeAI
UNIT Contractor Myrmidon Sanz Level(3, Enemy, True) [23, 5] 0 0 0 0 [IronSword] ChargeAI
UNIT Contractor Brigand Sanz Level(3, Enemy, True) [23, 17] DropItem 0 0 0 [Bloodaxe] ChargeAI
UNIT

SuperbossGang:
UNIT Yurena Champion Sanz Level(20, Enemy, False) [12, 21] 0 0 1 REDA12R20 [BraveLance, Saunion] BossAI
UNIT Sanz Sage Sanz Level(20, Enemy, False) [13, 21] 0 0 1 REDA13R20 [Tornado, Warp] BossAI
UNIT Orlando 0 Orlando Level(13, Enemy, False) [14, 21] 0 0 1 REDA14R20 [0] BossAI
UNIT

YurenaGaming:
UNIT Yurena Champion Sanz Level(20, Enemy, False) [14, 8] 0 0 0 0 [BraveLance, Saunion] ChargeAI
UNIT

SanzUndertaleEndCutscene:
UNIT Sanz Sage Sanz Level(20, Enemy, False) [18, 9] 0 0 0 0 [0] NoAI
UNIT Orlando 0 Orlando Level(13, Enemy, False) [18, 10] 0 0 0 0 [0] BossAI
UNIT Yurena Champion Sanz Level(20, Enemy, False) [19, 17] 0 0 0 0 [BraveLance, Saunion] BossAI

//generics that are running around at the beginning of this cutscene

UNIT Sentinel Hero Sanz Level(4, Enemy, True) [14, 12] 0 0 0 0 [IronBow] ChargeAI 
UNIT Sentinel Sniper Sanz Level(4, Enemy, True) [11, 7] 0 0 0 0 [IronBow] ChargeAI 
UNIT Swordmaster Swordmaster Sanz Level(4, Enemy, True) [11, 13] 0 0 0 0 [IronBow] ChargeAI 
UNIT GreagorPeddler General Sanz Level(4, Enemy, True) [21, 17] 0 0 0 0 [Javelin] BossAI
UNIT GreagorVillager General Sanz Level(4, Enemy, True) [23, 17] 0 0 0 0 [Javelin] BossAI
UNIT

AlonsoHimselfGroup:
UNIT GreagorSailor Trickster 0 Level(20, Enemy, False) [15, 17] 0 0 0 0 [0] NoAI //alonso, modify here once adventurer is complete
UNIT 

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
ENUT WentToHubA1
EVBIT_F 8 // For camera moving stuff
FADU 16
STAL 30
LOAD2 1 LuaDesiderioCutsceneUnits
ENUN
MOVE 20 Desiderio [5, 2] // Because dark before initial event sucks
MOVE 20 Lua [6, 2]
ENUN
Text(ChA1BeginningScene1)
CAMERA [26, 9]
CURSOR_FLASHING [26, 9]
CURSOR_FLASHING [27, 9]
STAL 60
REMOVE_CURSORS 
Text(ChA1BeginningScene2)
CAMERA [16, 17]
Text(ChA1BeginningScene3)
CAMERA Lua
LOAD1 0 IndioCutsceneUnit
ENUN
Text(ChA1BeginningScene4)
POPUP ChA1IndioRecruitPopup 0x5a // Item get! sfx, will hopefully get a def later
ENUT A1_EscapeDisallowed
ENUT AllUnitsMustEscapeFlag
LOAD1 1 ContractorInitialEnemies
ENUN
LOAD1 1 SentinelInitialEnemies
ENUN
TILECHANGE 0x8
TILECHANGE 0x6
GotoPrepScreen
NoFade
ENDA

InformObjective:
CAMERA_CENTERED [18, 7]
CURSOR_FLASHING [18, 7]
STAL 60
REMOVE_CURSORS
Text(ChA1InformObjectiveDialogue)
CAMERA_CENTERED [5, 3]
CURSOR_FLASHING [5, 2]
CURSOR_FLASHING [6, 3]
STAL 60
REMOVE_CURSORS
CAMERA_CENTERED [26, 8]
CURSOR_FLASHING [26, 8]
CURSOR_FLASHING [27, 8]
STAL 60
REMOVE_CURSORS
NoFade
ENDA

LuaNoticesChests:
CameraCursor(6,15)
CameraCursor(22,15)
CameraCursor(17,4)
CameraCursor(Lua)
Text(ChA1LuaNoticesChests)
NoFade
ENDA 


CarmenIndioTalk:
Text(ChA1CarmenIndioTalkDialogue)
NoFade
ENDA


PilarRazvanTalk:
Text(ChA1PilarRazvanTalkDialogue)
NoFade
ENDA


GeraldoLeaves:
CAMERA_CENTERED [11, 5]
CURSOR_FLASHING [11, 5]
STAL 60
REMOVE_CURSORS
Text(ChA1GeraldoLeavesDialogue)
MOVE 60 Geraldo [9, 4]
ENUN
DISA Geraldo
NoFade
ENDA


LuaRecruitLeandra:
CHECK_EVENTID A1_LeandraTalkSeen
BNE 1 sC s0
Text(ChA1LuaLeandraTalkDialogue)
ENUT A1_LeandraTalkSeen
LABEL 1
SVAL s1 1000
CHECK_MONEY
BLT 2 sC s1
Text(ChA1LeandraRecruitPrompt)
SVAL s1 0x1
BNE 2 s1 sC
MUSS EmergencyCrewmateID
Text(ChA1LeandraRecruitedDialogue)
SVAL s3 1000
TAKEMONEY
CUSA Leandra
MURE 2
GOTO 3
LABEL 2
ENUF A1_LeandraRecruited
POPUP ChA1LeandraRecruitPopup 0x5a // Item get! sfx, will hopefully get a def later
LABEL 3
NoFade
ENDA

DesiderioRecruitLeandra:
CHECK_EVENTID A1_LeandraTalkSeen
BNE 4 sC s0
Text(ChA1DesiderioLeandraTalkDialogue)
ENUT A1_LeandraTalkSeen
SVAL s1 1000
CHECK_MONEY
BLT 5 sC s1
LABEL 4
Text(ChA1LeandraRecruitPrompt)
SVAL s1 0x1
BNE 5 s1 sC
MUSS EmergencyCrewmateID
Text(ChA1LeandraRecruitedDialogue)
SVAL s3 1000
TAKEMONEY 
CUSA Leandra
MURE 2
GOTO 6
LABEL 5
ENUF A1_LeandraRecruited
POPUP ChA1LeandraRecruitPopup 0x5a // Item get! sfx, will hopefully get a def later
LABEL 6
NoFade
ENDA

SuperbossCameo:
CAMERA_CENTERED [13, 20]
STAL 30
LOAD1 1 SuperbossGang
ENUN
Text(ChA1SanzOrlandoCameoDialogue1)
MOVE 60 Yurena [12, 19]
ENUN
Text(ChA1SanzOrlandoCameoDialogue2)
MOVE 60 Yurena [12, 21]
MOVE 60 Sanz [13, 21]
MOVE 60 Orlando [14, 21]
ENUN
DISA Yurena
DISA Sanz
DISA Orlando
NoFade
ENDA

FirstTimerWarning:
Text(ChA1FirstTimerWarningDialogue)
NoFade
ENDA

SecondTimerWarning:
Text(ChA1SecondTimerWarningDialogue)
NoFade
ENDA

SuperbossesAppear:
CAMERA_CENTERED [13, 20]
STAL 30
LOAD1 1 SuperbossGang
ENUN
Text(ChA1SanzOrlandoAppearDialogue)
NoFade
ENDA

AlarmRaised:
Text(ChA1AlarmRaisedDialogue)
LOAD1 1 UndergroundReinforcements
CAMERA_CENTERED [4, 4]
CURSOR_FLASHING [4, 4]
STAL 60
REMOVE_CURSORS
Text(ChA1GeraldoReappearsDialogue)
NoFade
ENDA

YurenaDropping:
CAMERA_CENTERED [13, 20]
STAL 30
Text(ChA1YurenaWarpDialogue)
WARPOUT [12, 20]
DISA Yurena
ENDWARP
CAMERA_CENTERED [14, 8]
WARPIN [14, 8]
LOAD1 1 YurenaGaming
ENUN
NoFade
ENDA

SwitchObjective:
Text(ChA1RelicRecoveredDialogue)
AddTrap(BlueSigilTrapID, 5, 2)
AddTrap(BlueSigilTrapID, 6, 2)
AddTrap(BlueSigilTrapID, 26, 8)
AddTrap(BlueSigilTrapID, 27, 8)
ENUF A1_EscapeDisallowed
NoFade
ENDA

EscapeEventCheck:
ENUF A1_EscapeDisallowed
NoFade
ENDA

CheckNoUnits:
ASMC GetBlueUnitCountASMC
BNE 1 s0 sC
ENUT 0x03
CALL EndingScene
LABEL 1
NoFade
ENDA

EndingScene:
FADI 30
MUSC 0x7FFF
SetBackground(Ship)
CHECK_ALIVE Indio
BEQ 8 0x0 0xC
Text(ChA1EndingDialogue1IndioAlive)
GOTO 9
LABEL 8
Text(ChA1EndingDialogue1IndioDead)
LABEL 9
CHECK_ALIVE Leandra
BEQ 10 0x0 0xC
Text(ChA1EndingDialogueLeandraRecruited)

LABEL 10
EVBIT_F 2
FADI 30
CLEE 0 //clear all units for upcoming cutscene
CLEN 0
CLEA 0

CAMERA_CENTERED [16,11]
Music(SwabbieTalkID)
LOAD1 1 SanzUndertaleEndCutscene
ClearBackground
FADU 255
STAL 30
MOVE 6 GreagorPeddler [21, 5]
MOVE 6 GreagorVillager [23, 5]
ENUN
FlashCursor(Sanz, 60)
Text(ChA1EndingDialogue2)

MOVE 10 Orlando [17,15]
ENUN
MOVE 12 Orlando [5,15]
MOVE 8 Yurena [17,10]
ENUN
STAL 10
Music(0x7FFF)
STAL 30
FlashCursor(Yurena, 60)
Text(ChA1EndingDialogue3)

MOVE 24 Yurena [17,17]
ENUN
STAL 30
LOAD1 1 AlonsoHimselfGroup
Music(ABlackheartsPrideID)
MOVE 8 GreagorSailor [18, 10]
ENUN
FlashCursor(Sanz, 50)
Text(ChA1EndingDialogue4)
GoToHubChapter(HubA)
NoFade
ENDA

Dunno1: //down here because nothing here is really used
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN